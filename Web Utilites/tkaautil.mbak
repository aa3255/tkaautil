TKAAUTIL ;Utility Routines ;04/07/2015 16:12:31 ;Apr 6 2015 ;AA
 ; ADS Corp. Copyright
 q 
 ;
getdefaultdir()
 n path
 s path=##class(%File).ManagerDirectory()
 s path=$p(path,"\",1,$l(path,"\")-2)_"\"
 s path=path_"ADS\tkaautil\"
 i '##class(%File).DirectoryExists(path) Q ""
 q path
 ;
prepdir()
 n path,%SYSDIR,file,i,dirlist,a,tmp,cf,cnt
 s path=$$getdefaultdir(),cf=""
 k ^tkaautil("temp",$j)
 s file=path_"*"
r s file=$zse(file) s tmp=$p(file,"\",$l(file,"\"))
 i file]"",tmp'=".",tmp'=".." d  
 . i ##class(%File).DirectoryExists(file) d
 .. s cnt=$o(^tkaautil("temp",$j,"folders",""),-1)+1
 .. s ^tkaautil("temp",$j,"folders",cnt)=file
 . i '##class(%File).DirectoryExists(file) d
 .. s cnt=$o(^tkaautil("temp",$j,"files",""),-1)+1
 .. s ^tkaautil("temp",$j,"files",cnt)=file
 f i=1:1 d  q:file="" 
 . s file=$zse("") i file="" q
 . s tmp=$p(file,"\",$l(file,"\"))
 . i file]"",tmp'=".",tmp'=".." d
 .. i '##class(%File).DirectoryExists(file) d
 ... s cnt=$o(^tkaautil("temp",$j,"files",""),-1)+1
 ... s ^tkaautil("temp",$j,"files",cnt)=file
 .. i ##class(%File).DirectoryExists(file) d
 ... s cnt=$o(^tkaautil("temp",$j,"folders",""),-1)+1
 ... s ^tkaautil("temp",$j,"folders",cnt)=file
 s cf=$o(^tkaautil("temp",$j,"folders",cf)) i cf]"" s file=^(cf)_"\*" g r
 q $j
 ;
loadroutines()
 n job s job=$$prepdir() i 'job w job q
 n n,file,ext,list,filename,datemodified,i
 s n=0 f  s n=$o(^tkaautil("temp",job,"files",n)) q:'n  d
 . s file=^tkaautil("temp",job,"files",n)
 . s filename=##class(%File).GetFilename(file)
 . s ext=$p(filename,".",$l(filename,"."))
 . s name=$zcvt($p(filename,"."),"u")
 . i ext'="m" q
 . i $l(filename,".")>2 q
 . i '##class(%File).GetFileSize(file) q
 . s datemodified=##class(%File).GetFileDateModified(file)
 . i $d(^ROUTINE(name)),$tr(datemodified,",")<=$tr(^ROUTINE(name,0),",") q
 . s list=$g(list)_"*"_$$writetemp(file)
 i $g(list)]"" d 
 . d $system.OBJ.Load($e(list,2,*),"cukdil")
 . f i=1:1:$l(list,"*")  d ##class(%File).Delete($p(list,"*",i))
 q
 ;
read(file)
 n stream,i,rec,cnt
 s stream=##class(%FileCharacterStream).%New()
 s stream.Filename=file
 k ^tkaautil("temploaded",$j,file)
 f i=1:1 q:stream.AtEnd  d
 . s rec=stream.ReadLine()
 . s cnt=$o(^tkaautil("temploaded",$j,file,""),-1)+1
 . s ^tkaautil("temploaded",$j,file,cnt)=rec
 q "^tkaautil(""temploaded"""_","_$j_","_""""_file_""""_")"
 ;
writetemp(file)
 n tmpfile,a,newfile,name,datecreated,datemodified,l
 n %TIM,SYSNAM,EXTS,DESC
 s (IOPAR,DESC)="",EXT="INT"
 s a=$$read(file)
 s name=##class(%File).GetFilename(file)
 s name=$zcvt($p(name,"."),"u")
 s datecreated=##class(%File).GetFileDateCreated(file)
 s datemodified=##class(%File).GetFileDateModified(file)
 d INT^%T s %TIM=$zd($h,2,,4)_"  "_%TIM,SYSNAM=$System.Version.GetProduct()
 s @a@(0.1)=SYSNAM_"^"_EXT_"^"_DESC_"^~Format=Cache."_$s(IOPAR["V":"V",1:"S")_"~"_"^"_"RAW"
 s @a@(0.2)="%RO on "_%TIM
 s @a@(0.3)=name_"^"_EXT_"^"_1_"^"_$h_"^"_0
 s @a@(0.4)=name_" ;"_@a@(1)_" ;"_$zdt(datemodified,1)_" ;"_$zd(+datecreated,6)_" ;AA [Generated by TKAAUTIL]"
 s @a@(1)=" ; ADS Corp. Copyright"
 s tmpfile=##class(%File).TempFilename()
 s newfile=##class(%File).%New(tmpfile)
 d newfile.Open("WSN")
 s l="" f  s l=$o(@a@(l)) q:l=""  d newfile.WriteLine(^(l))
 k @a q tmpfile
 ;
MON d loadroutines() g MON
 q 